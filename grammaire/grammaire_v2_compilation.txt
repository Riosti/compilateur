programme             -> PROG corps

corps                 -> liste_declarations liste_instructions
corps                 -> liste_instructions

liste_declarations    -> declaration 
liste_declarations    -> liste_declarations POINT_VIRGULE declaration

liste_instructions    -> DEBUT suite_liste_inst FIN

suite_liste_inst      -> instruction
suite_liste_inst      -> suite_liste_inst POINT_VIRGULE instruction

declaration           -> declaration_type
declaration           -> declaration_variable
declaration           -> declaration_procedure
declaration           -> declaration_fonction

declaration_type      -> TYPE IDF DEUX_POINTS suite_declaration_type

suite_declaration_type -> STRUCT liste_champs FSTRUCT
suite_declaration_type -> TABLEAU dimension DE nom_type

dimension             -> CROCHET_OUVRANT liste_dimensions CROCHET_FERMANT

liste_dimensions      -> une_dimension
liste_dimensions      -> liste_dimensions VIRGULE une_dimension

une_dimension         -> expression POINT POINT expression

liste_champs          -> un_champ
liste_champs          -> liste_champs POINT_VIRGULE un_champ

un_champ              -> IDF DEUX_POINTS nom_type

nom_type              -> type_simple
nom_type              -> IDF

type_simple           -> ENTIER
type_simple           -> REEL
type_simple           -> BOOLEEN
type_simple           -> CARACTERE
type_simple           -> CHAINE CROCHET_OUVRANT CSTE_ENTIERE CROCHET_FERMANT

declaration_variable  -> VARIABLE IDF DEUX_POINTS nom_type

declaration_procedure -> PROCEDURE IDF liste_parametres corps

declaration_fonction  -> FONCTION IDF liste_parametres RETOURNE type_simple corps

liste_parametres      ->
liste_parametres      -> PARENTHESE_OUVRANTE liste_param PARENTHESE_FERMANTE

liste_param           -> un_param
liste_param           -> liste_param POINT_VIRGULE un_param

un_param              -> IDF DEUX_POINTS type_simple

instruction           -> affectation
instruction           -> condition
instruction           -> tant_que
instruction           -> appel
instruction           -> VIDE
instruction           -> RETOURNE resultat_retourne

resultat_retourne     ->
resultat_retourne     -> expression
resultat_retourne     -> expression_chaine /* modifié obligé sinon ambi*/

expression_chaine     -> expression_chaine PLUS CSTE_CHAINE
expression_chaine     -> CSTE_CHAINE

appel                 -> IDF liste_arguments

liste_arguments       ->
liste_arguments       ->  PARENTHESE_OUVRANTE liste_args PARENTHESE_FERMANTE

liste_args            -> un_arg
liste_args            -> liste_args VIRGULE un_arg

un_arg                -> expression

condition             -> SI expression
                         ALORS liste_instructions
                         SINON liste_instructions

tant_que              -> TANT_QUE expression FAIRE liste_instructions

affectation           -> variable OPAFF expression
affectation           -> variable OPAFF expression_chaine /*modifié*/
affectation           -> variable plus_moins		  /*modifié*/

variable              -> variable_idf
variable	      -> tableau
variable	      -> variable POINT tableau
variable	      -> variable POINT variable_idf

variable_idf	      -> IDF

tableau		      -> IDF CO param_du_tab CF
tableau		      -> tableau CO param_du_tab CF

param_du_tab	      -> expression
param_du_tab	      -> param_du_tab VIRGULE expression

expression            -> expression_et

expression_et	      -> expression_et ET expression_ou
expression_et	      -> expression_ou

expression_ou  	      -> expression_ou OU expression_neg
expression_ou  	      -> expression_neg

expression_neg	      -> NON expression_bcn
expression_neg        -> expression_bcn

expression_bcn        -> cste_bool
expression_bcn        -> expression_cmp
expression_bcn        -> expression_num

cste_bool	      -> VRAI
cste_bool	      -> FAUX

expression_cmp	      -> expression_num opcomp expression_num

opcomp	              -> INF
opcomp	              -> INFEGAL
opcomp	              -> SUP
opcomp	              -> SUPEGAL
opcomp	              -> EGAL
opcomp	              -> DIFF

expression_num	      -> e1

e1		      -> e2
e1		      -> e1 PLUS e2
e1		      -> e1 MOINS e2

e2		      -> e3
e2		      -> e2 MULT e3
e2		      -> e2 DIV e3

e3		      -> MOINS e4
e3		      -> e4

e4		      -> PO expression PF
e4		      -> CSTE_ENTIERE
e4		      -> CSTE_REEL
e4		      -> CSTE_CARACTERE
e4		      -> appel
e4		      -> e5

e5		      -> variable
e5		      -> variable plus_moins

plus_moins 	      -> INCREMENTE
plus_moins	      -> DECREMENTE
